{
  description = "NIXOS Configuration Flake";

  nixConfig = {
    extra-substituters = [
      "https://cache.numtide.com"
      "https://nix-community.cachix.org"
      "https://nix-gaming.cachix.org"
      "https://hyprland.cachix.org"
      "https://walker.cachix.org"
    ];
    extra-trusted-public-keys = [
      "niks3.numtide.com-1:DTx8wZduET09hRmMtKdQDxNNthLQETkc/yaX7M4qK0g="
      "nix-community.cachix.org-1:mB9FSh9qf2dCimDSUo8Zy7bkq5CX+/rkCWyvRCYg3Fs="
      "nix-gaming.cachix.org-1:nbjlureqMbRAxR1gJ/f3hxemL9svXaZF/Ees8vCUUs4="
      "hyprland.cachix.org-1:a7pgxzMz7+chwVL3/pzj6jIBMioiJM7ypFP8PwtkuGc="
      "walker.cachix.org-1:fG8q+uAaMqhsMxWjwvk0IMb4mFPFLqHjuvfwQxE4oJM="
    ];
  };

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-26.05";
    nixpkgs-unstable.url = "github:nixos/nixpkgs/nixos-unstable";

    agenix.url = "github:ryan4yin/ragenix";
    home-manager = {
      url = "github:nix-community/home-manager/release-26.05";
    };
    hyprland = {
      url = "github:hyprwm/Hyprland";
      inputs.nixpkgs.follows = "nixpkgs-unstable";
    };
    hyprland-plugins = {
      url = "github:hyprwm/hyprland-plugins";
      inputs.hyprland.follows = "hyprland";
    };
    impermanence.url = "github:nix-community/impermanence";
    lanzaboote = {
      url = "github:nix-community/lanzaboote/v1.0.0";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    llm-agents.url = "github:numtide/llm-agents.nix";
    nix-flatpak.url = "github:gmodena/nix-flatpak/";
    nix-gaming.url = "github:fufexan/nix-gaming";
    nixos-hardware.url = "github:NixOS/nixos-hardware/master";
    nixvim = {
      url = "github:nix-community/nixvim/nixos-26.05";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    terminaltexteffects = {
      url = "github:ChrisBuilds/terminaltexteffects/release-0.15.0";
      flake = false;
    };
    elephant.url = "github:abenz1267/elephant";
    walker = {
      url = "github:abenz1267/walker";
      inputs.elephant.follows = "elephant";
    };
  };

  outputs =
    inputs@{
      agenix,
      home-manager,
      hyprland,
      impermanence,
      lanzaboote,
      llm-agents,
      nixpkgs,
      nixpkgs-unstable,
      nixvim,
      self,
      ...
    }:
    let
      system = "x86_64-linux"; # The system type we will use
      pkgs = nixpkgs.legacyPackages.${system};

      specialArgs = {
        inherit agenix hyprland impermanence inputs lanzaboote llm-agents nixpkgs-unstable nixvim self;
      };

      commonModules = [
        agenix.nixosModules.default
        ./lib/nixos/secrets.nix
        home-manager.nixosModules.home-manager
        ({ config, ... }: {
          home-manager = {
            backupFileExtension = "backup";
            useGlobalPkgs = true;
            useUserPackages = true;
            extraSpecialArgs = specialArgs // { secrets = config.age.secrets; };
          };
        })
        ({ ... }: {
          nixpkgs.overlays = [];
        })
      ];

      mkNixosSystem = { name, username, modules, isServer ? false }:
        let
          hostname = name;
          hostArgs = specialArgs // { inherit hostname username isServer; };
        in
        nixpkgs.lib.nixosSystem {
          inherit system;
          specialArgs = hostArgs; # Pass the combined args
          modules = commonModules
            ++ modules
            ++ [
            ({ ... }: {
              nixpkgs.config.allowUnfree = true;
              home-manager.users.${username} = import ./users/${username}/default.nix {
                inherit (hostArgs) isServer;
                inherit (specialArgs) agenix inputs lib; # Explicitly inherit agenix, inputs, and lib from specialArgs
              };
            })
          ];
        };
    in
    {

      devShells.${system}.default = pkgs.mkShell {
        packages = [
          agenix.packages.${system}.default
          pkgs.nixpkgs-fmt
        ];
      };

      nixosConfigurations = {

        tahi = mkNixosSystem {
          name = "tahi";
          username = "factory";
          isServer = true;
          modules = [
            ./hosts/tahi/default.nix
            {
              system.stateVersion = "25.11";
            }
          ];
        };

        whio = mkNixosSystem {
          name = "whio";
          username = "factory";
          modules = [
            ./hosts/whio/default.nix
            {
              nixpkgs.overlays = [ llm-agents.overlays.default ];
              nixpkgs.config.permittedInsecurePackages = [
                "electron-39.8.10"
              ];
              system.stateVersion = "25.11";
            }
          ];
        };

      };

      # Standard outputs for convenience
      formatter.${system} = nixpkgs.legacyPackages.${system}.nixpkgs-fmt;
      packages.${system} = {
        nixos-system-tahi = self.nixosConfigurations.tahi.config.system.build.toplevel;
        nixos-system-whio = self.nixosConfigurations.whio.config.system.build.toplevel;
      };

    };

}
